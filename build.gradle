plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.4'
	id 'io.spring.dependency-management' version '1.1.4'
}

group = 'com.akfnt'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '21'
}

repositories {
	mavenCentral()
}

ext {
	set('springCloudVersion', "2023.0.1")
	set('testcontainersVersion', "1.19.3")
	set('testKeycloakVersion', "3.2.0")
}

// 프로젝트를 빌드할때 그래들이 설정 프로세서를 사용하도록 설정
configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
	runtimeOnly 'org.postgresql:postgresql'
	implementation 'org.springframework.cloud:spring-cloud-starter-config'
	implementation 'org.springframework.retry:spring-retry'
	// flyway: SQL 마이그레이션(src/main/resources/db/migration/) - for DDL, 자바 마이그레이션(src/main/java/db/migration/) for DML
	implementation 'org.flywaydb:flyway-core'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
	// 프로젝트 빌드시 새로운 속성에 대한 메타데이터가 자동으로 생성되고, META-INF/spring-configuration-metadata.json 에 저장된다
	// IDE 에서 해당 항목을 선택해 각 속성에 대한 설명 메시지를 표시할 수 있고, 자동 완성 및 유형 검사도 가능하다
	// Intellij Ultimate Edition 아닐 경우 https://github.com/ThomasVitale/cloud-native-spring-in-action/blob/main/Guides/configuring-intellij-idea.md 에서 추가 설정 필요
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	compileOnly 'org.projectlombok:lombok:1.18.32'
	annotationProcessor 'org.projectlombok:lombok:1.18.32'
	runtimeOnly 'io.micrometer:micrometer-registry-prometheus'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-webflux'
	testImplementation 'org.testcontainers:postgresql'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation "com.github.dasniko:testcontainers-keycloak:${testKeycloakVersion}"
	testCompileOnly 'org.projectlombok:lombok:1.18.32'
	testAnnotationProcessor 'org.projectlombok:lombok:1.18.32'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
		mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
	}
}

// 개발 환경을 위한 속성 정의
bootRun {
	// systemProperty 'spring.profiles.active', 'testdata'
	systemProperty 'polar.testdata.enabled', true
}

tasks.named('test') {
	useJUnitPlatform()
}

// 애플리케이션을 컨테이너화 하기 위한 설정
// 빌드팩을 사용해 OCI 이미지를 빌드하기 위한 스프링 부트 플러그인 작업
// 컨테이너 이미지를 빌드하고 깃허브 저장소에 업로드: ./gradlew bootBuildImage --imageName ghcr.io/akfnt/cns-catalog-service --publishImage -PregistryUrl="ghcr.io" -PregistryUsername=akfnt -PregistryToken=
bootBuildImage {
	// 빌드할 OCI 이미지의 이름. 이 이름은 그래들의 프로젝트 설정에서 정의한 이름과 같다.
	// 버전 번호를 사용하지 않고 '암묵적인 latest 태그'(prod 환경에서는 권장하지 않음)를 사용한다.
	imageName = "${project.name}"
	// 이미지에 설치할 JVM 버전. 자바 21을 사용한다.
	environment = ["BP_JVM_VERSION" : "21.*"]

	// 컨테이너 저장소 연결을 설정하기 위한 섹션
	docker {
		// 컨테이너 저장소 인증을 설정하기 위한 섹션. 값은 그래들 속성을 통해 전달된다.
		publishRegistry {
			username = project.findProperty("registryUsername")
			password = project.findProperty("registryToken")
			url = project.findProperty("registryUrl")
		}
	}
}